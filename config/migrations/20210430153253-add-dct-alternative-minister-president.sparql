PREFIX dct: <http://purl.org/dc/terms/>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX org: <http://www.w3.org/ns/org#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

INSERT {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?mandataris1 dct:alternative "Minister-president".
    ?mandataris2 dct:alternative "Minister-president".
  }
} WHERE {
  GRAPH <http://mu.semte.ch/graphs/public> {
    ?mandataris1 rdf:type mandaat:Mandataris;
                mandaat:start ?start1;
                org:holds ?mandaat1;
                mandaat:isBestuurlijkeAliasVan ?person.
    ?mandaat1 org:role ?rol1.
    ?rol1 skos:prefLabel ?label1.
    FILTER(LCASE(?label1) = "minister")
    ?mandataris2 mandaat:isBestuurlijkeAliasVan ?person;
                mandaat:start ?start2;
                org:holds ?mandaat2.
    ?mandaat2 org:role ?rol2.
    ?rol2 skos:prefLabel ?label2.
    FILTER(LCASE(?label2) = "minister-president")
    FILTER(?start1 >= ?start2)
    OPTIONAL {?mandataris1 mandaat:einde ?einde1.}
    OPTIONAL {?mandataris2 mandaat:einde ?einde2.}
    FILTER((!bound(?einde1) && !bound(?einde2)) || !bound(?einde2) || ?einde1 <= ?einde2)
  }
}